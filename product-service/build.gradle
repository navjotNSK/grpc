plugins {
    id 'java'
    id "com.google.protobuf" version "0.8.12"
}

group 'dev.techdozo.grpc'
version '1.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    //gRPC dependencies
    implementation 'com.google.protobuf:protobuf-java:3.12.4'
    implementation 'io.grpc:grpc-all:1.31.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //Lombok dependencies -
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    //Logging
    implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/proto_genarated'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.4'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.31.0"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                java {
                    outputSubDir = 'proto_genarated'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'proto_genarated'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen {
    doFirst {
        delete("$projectDir/src/main/proto_genarated")
    }
}
clean.dependsOn cleanProtoGen

